buildscript {
    repositories {
        jcenter()

    }
    dependencies {
        // Check for the latest version here: http://plugins.gradle.org/plugin/com.jfrog.artifactory
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4+"
    }
}

allprojects {
    apply plugin: "com.jfrog.artifactory"
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8
targetCompatibility = 1.8

def libraryGroupId = 'com.reynke.sloud'
def libraryArtifactId = 'users'
def libraryVersion = '1.0.0'
def libraryMainClassName = 'UsersPlugin'

group libraryGroupId + '.' + libraryArtifactId
version libraryVersion
mainClassName = group + '.' + libraryMainClassName

repositories {
    mavenCentral()
    maven {
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}

configurations {
    // Configuration that holds jars to include in the jar
    extraLibs
}

dependencies {
    compile group: 'org.spigotmc', name: 'spigot-api', version: '1.12-R0.1-SNAPSHOT'
    compile group: 'com.reynke.sloud.core', name: 'Core', version: '1.0.0'
    testCompile group: 'junit', name: 'junit', version: '4.11'
    configurations.compile.extendsFrom(configurations.extraLibs)
}

jar {
    manifest {
        attributes 'Implementation-Title': rootProject.name,
                'Implementation-Version': version,
                'Main-Class': mainClassName
    }

    from {
        configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

artifactory {
    contextUrl = "https://artifactory.reynke.com/artifactory"
    publish {
        repository {
            repoKey = version.endsWith('SNAPSHOT') ? 'gradle-dev-local' : 'gradle-release-local'
            username = reynke_artifactory_username
            password = reynke_artifactory_password
        }
        defaults {
            publications('mavenJava')
        }
    }
    resolve {
        repository {
            repoKey = 'gradle-all'
            username = reynke_artifactory_username
            password = reynke_artifactory_password
            maven = true
        }
    }
}

artifactoryPublish {
    dependsOn jar
}
